%% @author Couchbase <info@couchbase.com>
%% @copyright 2015 Couchbase, Inc.
%%
%% Licensed under the Apache License, Version 2.0 (the "License");
%% you may not use this file except in compliance with the License.
%% You may obtain a copy of the License at
%%
%%      http://www.apache.org/licenses/LICENSE-2.0
%%
%% Unless required by applicable law or agreed to in writing, software
%% distributed under the License is distributed on an "AS IS" BASIS,
%% WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
%% See the License for the specific language governing permissions and
%% limitations under the License.
%%

%%
%% Simple KV storage using ETS table as front end and file as the back end
%% for persistence.
%% Initialize using simple_store:start_link([your_store_name]).
%% Current consumer is XDCR checkpoints.
%%
-module(simple_store).

-include("ns_common.hrl").

%% APIs

-export([start_link/1,
         get/2, get/3,
         set/3,
         delete/2,
         delete_matching/2,
         iterate_matching/2]).

%% Macros

%% Persist the ETS table to file after 10 secs.
%% All updates to the table during that window will automatically get batched
%% and flushed to the file together.
-define(FLUSH_AFTER, 10 * 1000).

%% Max number of unsucessful flush attempts before giving up.
-define(FLUSH_RETRIES, 10).

%% Exported APIs

start_link(StoreName) ->
    ProcName = get_proc_name(StoreName),
    work_queue:start_link(ProcName, fun init/1, [StoreName]).

get(StoreName, Key) ->
    get(StoreName, Key, false).

get(StoreName, Key, Default) ->
    case ets:lookup(StoreName, Key) of
        [{Key, Value}] ->
            Value;
        [] ->
            Default
    end.

set(StoreName, Key, Value) ->
    do_work(StoreName, fun update_store/2, [Key, Value]).

delete(StoreName, Key) ->
    do_work(StoreName, fun delete_from_store/2, [Key]).

%% Delete keys with matching prefix
delete_matching(StoreName, KeyPattern) ->
    do_work(StoreName, fun del_matching/2, [KeyPattern]).

%% Return keys with matching prefix
iterate_matching(StoreName, KeyPattern) ->
    ets:foldl(
      fun ({Key, Value}, Acc) ->
              case misc:is_prefix(KeyPattern, Key) of
                  true ->
                      ?log_debug("Returning Key ~p. ~n", [Key]),
                      [{Key, Value} | Acc];
                  false ->
                      Acc
              end
      end, [], StoreName).

%% Internal
init(StoreName) ->
    %% Initialize flush_pending to false.
    erlang:put(flush_pending, false),

    %% Populate the table from the file if the file exists otherwise create
    %% an empty table.
    FilePath = path_config:component_path(data, get_file_name(StoreName)),
    Read =
        case filelib:is_regular(FilePath) of
            true ->
                ?log_debug("Reading ~p content from ~s", [StoreName, FilePath]),
                case ets:file2tab(FilePath, [{verify, true}]) of
                    {ok, StoreName} ->
                        true;
                    {error, Error} ->
                        ?log_debug("Failed to read ~p content from ~s: ~p",
                                   [StoreName, FilePath, Error]),
                        false
                end;
            false ->
                false
        end,

    case Read of
        true ->
            ok;
        false ->
            ?log_debug("Creating Table:~p ~n", [StoreName]),
            ets:new(StoreName, [named_table, set, protected]),
            ok
    end.

do_work(StoreName, Fun, Args) ->
    work_queue:submit_sync_work(
      get_proc_name(StoreName),
      fun () ->
              Fun(StoreName, Args)
      end).

%% Update the ETS table and schedule a flush to the file.
update_store(StoreName, [Key, Value]) ->
    ?log_debug("Updating data ~p in table ~p. ~n", [[{Key, Value}], StoreName]),
    ets:insert(StoreName, [{Key, Value}]),
    schedule_flush(StoreName, ?FLUSH_RETRIES).

%% Delete from the ETS table and schedule a flush to the file.
delete_from_store(StoreName, [Key]) ->
    ?log_debug("Deleting key ~p in table ~p. ~n", [Key, StoreName]),
    ets:delete(StoreName, Key),
    schedule_flush(StoreName, ?FLUSH_RETRIES).

del_matching(StoreName, [KeyPattern]) ->
    ets:foldl(
      fun ({Key, _}, _) ->
              case misc:is_prefix(KeyPattern, Key) of
                  true ->
                      ?log_debug("Deleting Key ~p. ~n", [Key]),
                      ets:delete(StoreName, Key);
                  false ->
                      ok
              end
      end, undefined, StoreName),
    schedule_flush(StoreName, ?FLUSH_RETRIES).

%% Nothing can be done if we failed to flush repeatedly.
schedule_flush(StoreName, 0) ->
    ?log_debug("Tried to flush table ~p ~p times but failed. Giving up.~n",
               [StoreName, ?FLUSH_RETRIES]),
    exit(flush_failed);

%% If flush is pending then nothing else to do otherwise schedule a
%% flush to the file for later.
schedule_flush(StoreName, NumRetries) ->
    case erlang:get(flush_pending) of
        true ->
            ?log_debug("Flush is already pending.~n"),
            ok;
        false ->
            erlang:put(flush_pending, true),
            {ok, _} = timer2:apply_after(?FLUSH_AFTER, work_queue, submit_work,
                                         [self(),
                                          fun () ->
                                                  flush_table(StoreName, NumRetries)
                                          end]),
            ?log_debug("Successfully scheduled a flush to the file.~n"),
            ok
    end.

%% Flush the table to the file.
flush_table(StoreName, NumRetries) ->
    %% Reset flush pending.
    erlang:put(flush_pending, false),
    FilePath = path_config:component_path(data, get_file_name(StoreName)),
    ?log_debug("Persisting Table ~p to file ~p. ~n", [StoreName, FilePath]),
    case ets:tab2file(StoreName, FilePath, [{extended_info, [object_count]}]) of
        ok ->
            ok;
        {error, Error} ->
            ?log_debug("Failed to persist table ~p to file ~p with error ~p. ~n",
                       [StoreName, FilePath, Error]),
            %% Reschedule another flush.
            schedule_flush(StoreName, NumRetries - 1)
    end.

get_proc_name(StoreName) ->
    list_to_atom(get_file_name(StoreName)).

get_file_name(StoreName) ->
    atom_to_list(?MODULE) ++ "_" ++ atom_to_list(StoreName).
